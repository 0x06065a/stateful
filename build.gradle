plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.5'
}

group = 'ru.stereohorse'
version = '1.0.1'

bintray {
    user = project.hasProperty('bintray_user') ? project.property('bintray_user') : System.getenv('bintray_user')
    key = project.hasProperty('bintray_key') ? project.property('bintray_key') : System.getenv('bintray_key')

    dryRun = true

    pkg {
        repo = 'maven'
        desc = 'FSM implementation'
        licenses = ['MIT']
        websiteUrl = 'https://github.com/0x06065a/stateful.git'
        vcsUrl = 'https://github.com/0x06065a/stateful.git'
        labels = ['fsm']
        publications = ['bintrayPublication']

        //noinspection GrReassignedInClosureLocalVar
        name = "${project.name}"

        //noinspection GroovyAssignabilityCheck
        version {
            name = "${project.version}"
            released  = new Date()
        }
    }
}

publishing {
    publications {
        bintrayPublication(MavenPublication) {
            from components.java
            groupId "${project.group}"
            artifactId "${project.name}"

            //noinspection GroovyAssignabilityCheck
            version "${project.version}"
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    compile 'org.projectlombok:lombok:1.16.6'
    compile 'org.slf4j:slf4j-api:1.7.13'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
}
